:PROPERTIES:
:ID:       5ea43242-8a75-4193-b000-1969a930a217
:END:
# SPDX-FileCopyrightText: 2024 László Vaskó <vlaci@fastmail.com>
#
# SPDX-License-Identifier: EUPL-1.2
#+title: Theming

My desktop Background image is a famous wallpaper from [[https://unsplash.com/photos/aerial-photography-of-mountain-and-river-uOi3lg8fGl4][Unsplash by Samuel Ferrara]]:
[[../assets/aerial-photography-of-mountain-and-river.jpg]]

To achieve consistent theming, I am using the [[https://github.com/danth/stylix][Stylix]] module:

#+caption: flake.nix:inputs
#+begin_src nix :noweb-ref flake-inputs
stylix.url = "github:danth/stylix";
#+end_src

It provides a NixOS module which also auto-loads Home-Manager integration: 

#+caption: nixos
#+begin_src nix :noweb no-export :noweb-ref nixos-modules
inputs.stylix.nixosModules.stylix
#+end_src

#+caption: nixos
#+begin_src nix :noweb-ref nixos-modules :prologue "(" :epilogue ")" :hl_lines /cz-Hickson-cursors/ /berkeley-mono-typeface/
{ lib, pkgs, ... }:

{
  stylix = {
    enable = true;
    base16Scheme = "${pkgs.base16-schemes}/share/themes/rose-pine-moon.yaml";
    image = ../assets/aerial-photography-of-mountain-and-river.jpg;
    cursor = {
      package = pkgs.cz-Hickson-cursors;
      name = "cz-Hickson-white";
      size = 24;
    };
    fonts = {
      monospace = {
        package = pkgs.berkeley-mono-typeface;
        name = "Berkeley Mono";
      };
    };
  };

  boot.plymouth.enable = true;

  home-manager.sharedModules = [
    {
      dconf.settings."org/gnome/desktop/interface".color-scheme = lib.mkForce "prefer-dark";
    }
    {
      stylix.iconTheme = {
        enable = true;
        package = pkgs.papirus-icon-theme;
        dark = "Papirus-Dark";
        light = "Papirus-Light";
      };
    }
  ];

  specialisation.day.configuration = {
    stylix = {
      polarity = lib.mkForce "light";
      base16Scheme = lib.mkForce "${pkgs.base16-schemes}/share/themes/rose-pine-dawn.yaml";
      cursor.name = lib.mkForce "cz-Hickson-black";
    };
    home-manager.sharedModules = [
      {
        dconf.settings."org/gnome/desktop/interface".color-scheme = lib.mkOverride 49 "prefer-light";
      }
    ];
  };
}
#+end_src

The cursor and monospace font configured are not part of =nixpkgs=, their definition are [[id:1d4a469a-4995-4515-bdde-683109afdd8b][cz-Hickson Cursors]] and [[id:b3bd76f7-3a6a-4129-8438-93f2d58653fb][Berkeley Mono Font]].

#+caption: nixos
#+begin_src nix :noweb-ref nixos-modules :prologue "(" :epilogue ")"
{ pkgs, ... }:

{
  fonts.packages = [
    pkgs.nerd-fonts.symbols-only
  ];
}
#+end_src

=Stylix= currently doesn't support =Niri= background setting, so we need to do it ourselves:

#+caption: home-manager
#+begin_src nix :noweb-ref home-manager-modules :prologue "(" :epilogue ")"
{
  lib,
  pkgs,
  config,
  ...
}:

{
  programs.niri.settings = {
    spawn-at-startup = [
      {
        command = [
          (lib.getExe pkgs.swaybg)
          "--image"
          "${config.stylix.image}"
          "--mode"
          config.stylix.imageScalingMode
        ];
      }
    ];
  };
}
#+end_src

#+caption: home-manager
#+begin_src nix :noweb-ref home-manager-modules :prologue "(" :epilogue ")"
{
  lib,
  pkgs,
  config,
  ...
}:

{
  services.darkman = {
    enable = true;
    settings.usegeoclue = true;

    darkModeScripts.color-scheme-dark = ''
      doas /nix/var/nix/profiles/system/bin/switch-to-configuration test
      echo dark > $XDG_RUNTIME_DIR/color-scheme
    '';

    lightModeScripts.color-scheme-light = ''
      doas /nix/var/nix/profiles/system/specialisation/day/bin/switch-to-configuration test
      echo light > $XDG_RUNTIME_DIR/color-scheme
    '';
  };
}
#+end_src

#+caption: nixos
#+begin_src nix :noweb-ref nixos-modules
{
  security.doas.extraRules = [
    {
      groups = [ "wheel" ];
      cmd = "/nix/var/nix/profiles/system/bin/switch-to-configuration";
      args = [ "test" ];
      noPass = true;
      keepEnv = true;
    }
    {
      groups = [ "wheel" ];
      cmd = "/nix/var/nix/profiles/system/specialisation/day/bin/switch-to-configuration";
      args = [ "test" ];
      noPass = true;
      keepEnv = true;
    }
  ];
}
#+end_src

#+print_bibliography:
